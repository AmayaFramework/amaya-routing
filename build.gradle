import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'io.github.amayaframework'
archivesBaseName = 'amaya-routing'
version = System.getenv('ROUTING_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // Tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    // Dependencies
    api group: 'com.github.romanqed', name: 'jfunc', version: '1.1.4'
    api group: 'io.github.amayaframework', name: 'amaya-filter', version: '1.0.0'
    api group: 'io.github.amayaframework', name: 'amaya-router', version: '1.0.1'
    api group: 'io.github.amayaframework', name: 'amaya-path', version: '1.0.0'
    api group: 'io.github.amayaframework', name: 'amaya-path-parser', version: '1.0.0'
    // TODO Replace api with co
    /*compileOnly*/ api group: 'io.github.amayaframework', name: 'amaya-http', version: '1.0.0'
    /*compileOnly*/ api group: 'io.github.amayaframework', name: 'amaya-context', version: '1.1.2'
    /*compileOnly*/ api group: 'io.github.amayaframework', name: 'amaya-web', version: '1.0.0'
    // TODO Remove
    api group: 'io.github.amayaframework', name: 'amaya-jetty', version: '1.0.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        var date = new Date()
        var javaVersion = System.getProperty("java.version")
        var vmVendor = System.getProperty("java.vm.vendor")
        var vmVersion = System.getProperty("java.vm.version")

        attributes(
                'Created-By': String.format("%s (%s %s)", javaVersion, vmVendor, vmVersion),
                'Gradle-Version': "Gradle $gradle.gradleVersion",
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd").format(date),
                'Build-Time': new SimpleDateFormat("HH:mm:ss.SSSZ").format(date),
                'Built-By': 'Roman Bakaldin',
                'Bundle-Name': 'Amaya Routing',
                'Bundle-Version': archiveVersion,
                'Bundle-SymbolicName': archivesBaseName,
                'Implementation-Title': archivesBaseName,
                'Implementation-Vendor': 'github.com/amayaframework',
                'Implementation-Version': archiveVersion,
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': 'github.com/amayaframework',
                'Specification-Version': archiveVersion
        )
    }
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('A module of the amaya framework that implements routing of http paths ' +
                        'and processing of path and query parameters.')
                url.set('https://github.com/AmayaFramework/amaya-routing')

                organization {
                    name.set('io.github.amayaframework')
                    url.set('https://github.com/AmayaFramework')
                }

                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/AmayaFramework/amaya-routing/issues')
                }

                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/AmayaFramework/amaya-routing/blob/main/LICENSE')
                    }
                }

                scm {
                    url.set('https://github.com/AmayaFramework/amaya-routing')
                    connection.set('scm:https://github.com/AmayaFramework/amaya-routing.git')
                    developerConnection.set('scm:https://github.com/AmayaFramework/amaya-routing.git')
                }

                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE1_USERNAME')
                password System.getenv('SONATYPE1_PASSWORD')
            }
        }
    }
}
